cmake_minimum_required(VERSION 3.0)
set(CMAKE_VERBOSE_MAKEFILE FALSE)
set(TD_BUILD_TAOSA_INTERNAL FALSE)
set(TD_BUILD_KEEPER_INTERNAL FALSE)

# set output directory
SET(LIBRARY_OUTPUT_PATH ${PROJECT_BINARY_DIR}/build/lib)
SET(EXECUTABLE_OUTPUT_PATH ${PROJECT_BINARY_DIR}/build/bin)
SET(TD_TESTS_OUTPUT_DIR ${PROJECT_BINARY_DIR}/test)

MESSAGE(STATUS "Project source directory: " ${PROJECT_SOURCE_DIR})
MESSAGE(STATUS "Project binary files output path: " ${PROJECT_BINARY_DIR})
MESSAGE(STATUS "Project executable files output path: " ${EXECUTABLE_OUTPUT_PATH})
MESSAGE(STATUS "Project library files output path: " ${LIBRARY_OUTPUT_PATH})

IF(NOT DEFINED TD_GRANT)
    SET(TD_GRANT FALSE)
ENDIF()

IF(NOT DEFINED BUILD_WITH_RAND_ERR)
    SET(BUILD_WITH_RAND_ERR FALSE)
ELSE()
    SET(BUILD_WITH_RAND_ERR TRUE)
ENDIF()

IF("${WEBSOCKET}" MATCHES "true")
    SET(TD_WEBSOCKET TRUE)
    MESSAGE("Enable websocket")
    ADD_DEFINITIONS(-DWEBSOCKET)
ELSE()
    SET(TD_WEBSOCKET FALSE)
ENDIF()

IF("${BUILD_HTTP}" STREQUAL "")
    IF(TD_LINUX)
        IF(TD_ARM_32)
            SET(TD_BUILD_HTTP TRUE)
        ELSE()
            SET(TD_BUILD_HTTP TRUE)
        ENDIF()
    ELSEIF(TD_DARWIN)
        SET(TD_BUILD_HTTP TRUE)
    ELSE()
        SET(TD_BUILD_HTTP TRUE)
    ENDIF()
ELSEIF(${BUILD_HTTP} MATCHES "false")
    SET(TD_BUILD_HTTP FALSE)
ELSEIF(${BUILD_HTTP} MATCHES "true")
    SET(TD_BUILD_HTTP TRUE)
ELSEIF(${BUILD_HTTP} MATCHES "internal")
    SET(TD_BUILD_HTTP FALSE)
    SET(TD_BUILD_TAOSA_INTERNAL TRUE)
ELSE()
    SET(TD_BUILD_HTTP TRUE)
ENDIF()

IF(TD_BUILD_HTTP)
    ADD_DEFINITIONS(-DHTTP_EMBEDDED)
ENDIF()

IF("${BUILD_KEEPER}" STREQUAL "")
    SET(TD_BUILD_KEEPER FALSE)
ELSEIF(${BUILD_KEEPER} MATCHES "false")
    SET(TD_BUILD_KEEPER FALSE)
ELSEIF(${BUILD_KEEPER} MATCHES "true")
    SET(TD_BUILD_KEEPER TRUE)
ELSEIF(${BUILD_KEEPER} MATCHES "internal")
    SET(TD_BUILD_KEEPER FALSE)
    SET(TD_BUILD_KEEPER_INTERNAL TRUE)
ELSE()
    SET(TD_BUILD_KEEPER FALSE)
ENDIF()

IF("${BUILD_TOOLS}" STREQUAL "")
    IF(TD_LINUX)
        IF(TD_ARM_32)
            SET(BUILD_TOOLS "false")
        ELSEIF(TD_ARM_64)
            SET(BUILD_TOOLS "false")
        ELSE()
            SET(BUILD_TOOLS "false")
        ENDIF()
    ELSEIF(TD_DARWIN)
        SET(BUILD_TOOLS "false")
    ELSE()
        SET(BUILD_TOOLS "false")
    ENDIF()
ENDIF()

IF("${BUILD_TOOLS}" MATCHES "false")
    MESSAGE("${Yellow} Will _not_ build taos_tools! ${ColourReset}")
    SET(TD_TAOS_TOOLS FALSE)
ELSE()
    MESSAGE("")
    MESSAGE("${Green} Will build taos_tools! ${ColourReset}")
    MESSAGE("")
    SET(TD_TAOS_TOOLS TRUE)
ENDIF()

IF(${TD_WINDOWS})
    SET(TAOS_LIB taos_static)
ELSE()
    SET(TAOS_LIB taos)
ENDIF()

# build TSZ by default
IF("${TSZ_ENABLED}" MATCHES "false")
    set(VAR_TSZ "" CACHE INTERNAL "global variant empty")
ELSE()
    # define add
    MESSAGE(STATUS "build with TSZ enabled")
    ADD_DEFINITIONS(-DTD_TSZ)
    set(VAR_TSZ "TSZ" CACHE INTERNAL "global variant tsz")
ENDIF()

# force set all platform to JEMALLOC_ENABLED = false
SET(JEMALLOC_ENABLED OFF)

IF(TD_WINDOWS)
    MESSAGE("${Yellow} set compiler flag for Windows! ${ColourReset}")

    IF(${CMAKE_BUILD_TYPE} MATCHES "Release")
        MESSAGE("${Green} will build Release version! ${ColourReset}")
        SET(COMMON_FLAGS "/W3 /D_WIN32 /DWIN32 /Zi- /O2 /GL /MD")

    ELSE()
        MESSAGE("${Green} will build Debug version! ${ColourReset}")
        SET(COMMON_FLAGS "/w /D_WIN32 /DWIN32 /Zi /MTd")
    ENDIF()

    SET(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /MANIFEST:NO")

    # IF (MSVC AND (MSVC_VERSION GREATER_EQUAL 1900))
    # SET(COMMON_FLAGS "${COMMON_FLAGS} /Wv:18")
    # ENDIF ()
    IF(CMAKE_DEPFILE_FLAGS_C)
        SET(CMAKE_DEPFILE_FLAGS_C "")
    ENDIF()

    IF(CMAKE_DEPFILE_FLAGS_CXX)
        SET(CMAKE_DEPFILE_FLAGS_CXX "")
    ENDIF()

    IF(CMAKE_C_FLAGS_DEBUG)
        SET(CMAKE_C_FLAGS_DEBUG "" CACHE STRING "" FORCE)
    ENDIF()

    IF(CMAKE_CXX_FLAGS_DEBUG)
        SET(CMAKE_CXX_FLAGS_DEBUG "" CACHE STRING "" FORCE)
    ENDIF()

    SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${COMMON_FLAGS}")
    SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${COMMON_FLAGS}")

ELSE()
    IF(${TD_DARWIN})
        set(CMAKE_MACOSX_RPATH 0)
    ENDIF()

    IF(${COVER} MATCHES "true")
        MESSAGE(STATUS "Test coverage mode, add extra flags")
        SET(GCC_COVERAGE_COMPILE_FLAGS "-fprofile-arcs -ftest-coverage")
        SET(GCC_COVERAGE_LINK_FLAGS "-lgcov --coverage")
        SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} ${GCC_COVERAGE_COMPILE_FLAGS} ${GCC_COVERAGE_LINK_FLAGS}")
        SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} ${GCC_COVERAGE_COMPILE_FLAGS} ${GCC_COVERAGE_LINK_FLAGS}")
    ENDIF()

    # disable all assert
    IF((${DISABLE_ASSERT} MATCHES "true") OR(${DISABLE_ASSERTS} MATCHES "true"))
        ADD_DEFINITIONS(-DDISABLE_ASSERT)
        MESSAGE(STATUS "Disable all asserts")
    ENDIF()

    INCLUDE(CheckCCompilerFlag)

    IF(TD_ARM_64 OR TD_ARM_32)
        SET(COMPILER_SUPPORT_SSE42 false)
    ELSEIF(("${CMAKE_C_COMPILER_ID}" MATCHES "Clang") OR("${CMAKE_C_COMPILER_ID}" MATCHES "AppleClang"))
        SET(COMPILER_SUPPORT_SSE42 true)
        MESSAGE(STATUS "Always enable sse4.2 for Clang/AppleClang")
    ELSE()
        CHECK_C_COMPILER_FLAG("-msse4.2" COMPILER_SUPPORT_SSE42)
    ENDIF()

    IF(TD_ARM_64 OR TD_ARM_32)
        SET(COMPILER_SUPPORT_FMA false)
        SET(COMPILER_SUPPORT_AVX false)
        SET(COMPILER_SUPPORT_AVX2 false)
        SET(COMPILER_SUPPORT_AVX512F false)
        SET(COMPILER_SUPPORT_AVX512BMI false)
        SET(COMPILER_SUPPORT_AVX512VL false)
    ELSE()
        CHECK_C_COMPILER_FLAG("-mfma" COMPILER_SUPPORT_FMA)
        CHECK_C_COMPILER_FLAG("-mavx512f" COMPILER_SUPPORT_AVX512F)
        CHECK_C_COMPILER_FLAG("-mavx512vbmi" COMPILER_SUPPORT_AVX512BMI)
        CHECK_C_COMPILER_FLAG("-mavx512vl" COMPILER_SUPPORT_AVX512VL)

        INCLUDE(CheckCSourceRuns)
        SET(CMAKE_REQUIRED_FLAGS "-mavx")
        check_c_source_runs("
        #include <immintrin.h>
        int main() {
          __m256d a, b, c;
          double buf[4] = {0};
          a = _mm256_loadu_pd(buf);
          b = _mm256_loadu_pd(buf);
          c = _mm256_add_pd(a, b);
          _mm256_storeu_pd(buf, c);
          for (int i = 0; i < sizeof(buf) / sizeof(buf[0]); ++i) {
            IF (buf[i] != 0) {
              return 1;
            }
          }
          return 0;
        }
      " COMPILER_SUPPORT_AVX)

        SET(CMAKE_REQUIRED_FLAGS "-mavx2")
        check_c_source_runs("
        #include <immintrin.h>
        int main() {
          __m256i a, b, c;
          int buf[8] = {0};
          a = _mm256_loadu_si256((__m256i *)buf);
          b = _mm256_loadu_si256((__m256i *)buf);
          c = _mm256_and_si256(a, b);
          _mm256_storeu_si256((__m256i *)buf, c);
          for (int i = 0; i < sizeof(buf) / sizeof(buf[0]); ++i) {
            IF (buf[i] != 0) {
              return 1;
            }
          }
          return 0;
        }
      " COMPILER_SUPPORT_AVX2)
    ENDIF()

    IF(COMPILER_SUPPORT_SSE42)
        SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -msse4.2")
        SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -msse4.2")
    ENDIF()

    IF("${SIMD_SUPPORT}" MATCHES "true")
        IF(COMPILER_SUPPORT_FMA)
            SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -mfma")
            SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -mfma")
            MESSAGE(STATUS "FMA instructions is ACTIVATED")
        ENDIF()

        IF(COMPILER_SUPPORT_AVX)
            SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -mavx")
            SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -mavx")
            MESSAGE(STATUS "AVX instructions is ACTIVATED")
        ENDIF()

        IF(COMPILER_SUPPORT_AVX2)
            SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -mavx2")
            SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -mavx2")
            MESSAGE(STATUS "AVX2 instructions is ACTIVATED")
        ENDIF()
    ENDIF()

    IF("${SIMD_AVX512_SUPPORT}" MATCHES "true")
        IF(COMPILER_SUPPORT_AVX512F AND COMPILER_SUPPORT_AVX512BMI)
            SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -mavx512f -mavx512vbmi")
            SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -mavx512f -mavx512vbmi")
            MESSAGE(STATUS "avx512f/avx512bmi enabled by compiler")
        ENDIF()

        IF(COMPILER_SUPPORT_AVX512VL)
            SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -mavx512vl")
            SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -mavx512vl")
            MESSAGE(STATUS "avx512vl enabled by compiler")
        ENDIF()
    ENDIF()

    # build mode
    SET(CMAKE_C_FLAGS_REL "${CMAKE_C_FLAGS} -Werror -Werror=return-type -fPIC -O3 -Wformat=2 -Wno-format-nonliteral -Wno-format-truncation -Wno-format-y2k")
    SET(CMAKE_CXX_FLAGS_REL "${CMAKE_CXX_FLAGS} -Werror -Wno-reserved-user-defined-literal -Wno-literal-suffix -Werror=return-type -fPIC -O3 -Wformat=2 -Wno-format-nonliteral -Wno-format-truncation -Wno-format-y2k")

    IF(${BUILD_SANITIZER})
        SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS}     -Werror -Werror=return-type -fPIC -gdwarf-2 -fsanitize=address -fsanitize=undefined -fsanitize-recover=all -fsanitize=float-divide-by-zero -fsanitize=float-cast-overflow -fno-sanitize=shift-base -fno-sanitize=alignment -g3 -Wformat=0")

        # SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-literal-suffix -Werror=return-type -fPIC -gdwarf-2 -fsanitize=address -fsanitize=undefined -fsanitize-recover=all -fsanitize=float-divide-by-zero -fsanitize=float-cast-overflow -fno-sanitize=shift-base -fno-sanitize=alignment -g3 -Wformat=0")
        SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-literal-suffix -Werror=return-type -fPIC -gdwarf-2 -fsanitize=address -fsanitize-recover=all -fsanitize=float-divide-by-zero -fsanitize=float-cast-overflow -fno-sanitize=shift-base -fno-sanitize=alignment -g3 -Wformat=0")
        MESSAGE(STATUS "Compile with Address Sanitizer!")
    ELSEIF(${BUILD_RELEASE})
        SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS_REL}")
        SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS_REL}")
    ELSE()
        SET(CMAKE_C_FLAGS "${CMAKE_C_FLAGS} -Werror -Werror=return-type -fPIC -g3 -gdwarf-2 -Wformat=2 -Wno-format-nonliteral -Wno-format-truncation -Wno-format-y2k")
        SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wno-reserved-user-defined-literal -g3 -Wno-literal-suffix -Werror=return-type -fPIC -gdwarf-2 -Wformat=2 -Wno-format-nonliteral -Wno-format-truncation -Wno-format-y2k")
    ENDIF()
ENDIF()
