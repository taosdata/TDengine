system sh/stop_dnodes.sh
system sh/deploy.sh -n dnode1 -i 1
system sh/exec.sh -n dnode1 -s start
sleep 50
sql connect

print =============== create database
sql create database d0 vgroups 1
sql select * from information_schema.ins_databases
if $rows != 3 then
  return -1
endi

print $data00 $data01 $data02

sql use d0

print =============== create super table, include column type for count/sum/min/max/first
sql create table if not exists stb (ts timestamp, k int) tags (a int)

sql show stables
if $rows != 1 then 
  return -1
endi

print =============== create child table
sql create table ct1 using stb tags(1000)
sql create table ct2 using stb tags(2000)
sql create table ct3 using stb tags(3000)

sql show tables
if $rows != 3 then 
  return -1
endi

sql create stream s1 trigger at_once into outstb as select _wstart, min(k), max(k), sum(k) as sum_alias from ct1 interval(10m)

run tsim/stream/checkTaskStatus.sim

sql show stables
if $rows != 2 then 
  return -1
endi

print =============== insert data

sql insert into ct1 values('2022-05-08 03:42:00.000', 234)
sleep 100

#===================================================================
print =============== query data from child table

$loop_count = 0

loop0:

sleep 1000

$loop_count = $loop_count + 1
if $loop_count == 10 then
  return -1
endi

sql select `_wstart`,`min(k)`,`max(k)`,sum_alias from outstb
print rows: $rows 
print $data00  $data01 $data02 $data03
if $rows != 1 then
  goto loop0
endi

if $data01 != 234 then 
  goto loop0
endi

if $data02 != 234 then 
  goto loop0
endi

if $data03 != 234 then 
  goto loop0
endi

#===================================================================
print =============== insert data

sql insert into ct1 values('2022-05-08 03:57:00.000', -111)
sleep 100


#===================================================================
print =============== query data from child table

$loop_count = 0

loop1:

sleep 1000

$loop_count = $loop_count + 1
if $loop_count == 10 then
  return -1
endi

sql select `_wstart`,`min(k)`,`max(k)`,sum_alias from outstb
print rows: $rows 
print $data00  $data01 $data02 $data03
print $data10  $data11 $data12 $data13
if $rows != 2 then
  goto loop1
endi

if $data01 != 234 then 
  goto loop1
endi

if $data02 != 234 then 
  goto loop1
endi

if $data03 != 234 then 
  goto loop1
endi

if $data11 != -111 then 
  goto loop1
endi

if $data12 != -111 then 
  goto loop1
endi

if $data13 != -111 then 
  goto loop1
endi

print step 2====================

sql create database test  vgroups 1 ;
sql use test;
sql create stable st(ts timestamp,a int,b int,c int) tags(ta int,tb int,tc int);
sql create table t1 using st tags(1,1,1);
sql create table t2 using st tags(2,2,2);

sql_error create stream streams1 trigger max_delay 4000a ignore update 0 ignore expired 0 into streamtST1 as select  _wstart, count(*) from st interval(5s);
sql_error create stream streams2 trigger max_delay 4s ignore update 0 ignore expired 0 into streamtST2 as select  _wstart, count(*) from st interval(5s);
sql create stream streams3 trigger max_delay 5000a ignore update 0 ignore expired 0 into streamtST3 as select  _wstart, count(*) from st interval(5s);
sql create stream streams4 trigger max_delay 5s ignore update 0 ignore expired 0 into streamtST4 as select  _wstart, count(*) from st interval(5s);


sql_error create stream streams5 trigger at_once ignore update 0 ignore expired 0 into streamtST5 as select  _wstart, count(*) from st interval(5s) having count(*) > 2;
sql_error create stream streams6 trigger at_once ignore update 0 ignore expired 0 into streamtST6 as select  _wstart, count(*) from st session(ts, 5s) having count(*) > 2;
sql_error create stream streams7 trigger at_once ignore update 0 ignore expired 1 into streamtST7 as select  _wstart, count(*) from st count_window(10) having count(*) > 2;
sql_error create stream streams8 trigger at_once ignore update 0 ignore expired 0 into streamtST8 as select  _wstart, count(*) from st state_window(a) having count(*) > 2;
sql_error create stream streams9 trigger at_once ignore update 0 ignore expired 0 into streamtST9 as select  _wstart, count(*) from st event_window start with a = 0 end with b = 9 having count(*) > 2;

system sh/exec.sh -n dnode1 -s stop -x SIGINT
